allprojects {
    group 'com.pia'
    version '0.1.0'

    apply plugin: 'java'

    sourceCompatibility = 1.8
}

subprojects {
    ext {
        distFolder = "/dist"
        distOutputEnabled = true
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        testCompile group: 'junit', name: 'junit', version: '4.12'
    }

    task copyJarToDist(type: Copy) {
        from jar
        into "${project.rootDir}/${project.ext.distFolder}"

        // Lazily override "into" after subprojects have been evaluated
        doFirst {
            into "${project.rootDir}/${project.ext.distFolder}"
        }
    }

    build.finalizedBy('copyJarToDist')
}

project(':cli') {
    dependencies {
        compile project(':core')
        runtime project(':plugins')
    }
}

project(':gui') {
    dependencies {
        compile project(':core')
        runtime project(':plugins')
    }
}

project(':core') {
    copyJarToDist.enabled = false
    dependencies {
        compile 'javax.validation:validation-api:2.0.0.Final'
    }
}

project(':plugins') {
    dependencies {
        compileOnly project(':core')
    }
}